class SnakeGame{
    field Snake snake;

    constructor SnakeGame new(){
        let snake = Snake.new();
        return this;
    }

    method void run(){
        var char currentKey;
        var char lastKey;
        var int moveCounter;
        var int direction;

        do drawBorder(8);
        do drawSnake();
        let moveCounter = 0;
        while(true){
            let direction = 0;
            let currentKey = Keyboard.keyPressed();
            
            if (~(currentKey = 0)){
                if (currentKey = 81){
                    return;
                }else{
                    let lastKey = currentKey;
                }
            }            
            if (moveCounter = 5){
                if (lastKey = 131) { let direction = 1; }   // up arrow
                if (lastKey = 133) { let direction = 2; }   // down arrow
                if (lastKey = 130) { let direction = 3; }   // left arrow
                if (lastKey = 132) { let direction = 4; }   // right arrow
                if (~(direction = 0)){
                    do snake.move(direction);
                    do drawMovingSnake();
                }
                let moveCounter = 0;
            } else {
                let moveCounter = moveCounter + 1;
            }
            do Sys.wait(100);
        }
        return;
    }

    method void drawSnake(){
        var CoordinateQueue coordinatesQueue;
        var Array coordinatesArray;
        var Coordinate currentCoordinate;
        var int size;
        var int i;
        
        let coordinatesQueue = snake.getCoordinates();
        let coordinatesArray = coordinatesQueue.getArray();
        let size = coordinatesQueue.getNrElements();
        let i = 0;

        do Screen.setColor(true);

        while (i < size){
            let currentCoordinate = coordinatesArray[i];
            do Screen.drawRectangle(currentCoordinate.getX()*8, currentCoordinate.getY()*8, (currentCoordinate.getX()*8)+8, (currentCoordinate.getY()*8)+8);
            let i = i + 1;
        }
        do Screen.setColor(false);
        return;
    }

    method void drawMovingSnake(){
        var Coordinate tail;
        var int tailX;
        var int tailY;

        let tail = snake.getTail();
        let tailX = tail.getX();
        let tailY = tail.getY();


        do Screen.setColor(false);
        do Screen.drawRectangle(tailX*8, tailY*8, (tailX*8)+8, (tailY*8)+8);

        do drawSnake();
        return;
    }

    method void drawBorder(int borderWidth){
        do Screen.setColor(true);
        do Screen.drawRectangle(0, 0, 511, borderWidth);                                // North Border
        do Screen.drawRectangle(0, 255-borderWidth, 511, 255);                          // South Border
        do Screen.drawRectangle(0, borderWidth+1, borderWidth, 255-borderWidth);        // West Border
        do Screen.drawRectangle(511-borderWidth, borderWidth+1, 511, 255-borderWidth);  // East Border
        do Screen.setColor(false);
        return;
    }

    method void dispose(){
        do Memory.deAlloc(this);
        return;
    }
    
}
