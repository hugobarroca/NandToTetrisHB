// This class emulates the snake game in the hack platform.
// It creates a board of 64 x 32 squares, with each square having an 8px long side.
class SnakeGame{
    field Snake snake;
    field Coordinate food;
    field int seed;
    field int score;

    // Creates a new SnakeGame.
    constructor SnakeGame new(){
        let snake = Snake.new();
        let seed = 1;
        let score = 0;
        return this;
    }

    method int run(){
        var Coordinate head;
        var char currentKey;
        var char lastKey;
        var int moveCounter;
        var int direction;
        var int eqX;
        var int eqY;
        var int feqX;
        var int feqY;
        var boolean collided;

        do drawBorder(8);
        do drawSnake();
        let moveCounter = 0;
        let direction = 4;

        do spawnFood();

        while(true){
            let currentKey = Keyboard.keyPressed();
            
            if (currentKey = 81){
                return score;
            }else{
                let lastKey = currentKey;
            }

            if (moveCounter = 2){
                if (lastKey = 131 & ~(direction = 2)) { let direction = 1; }   // up arrow
                if (lastKey = 133 & ~(direction = 1)) { let direction = 2; }   // down arrow
                if (lastKey = 130 & ~(direction = 4)) { let direction = 3; }   // left arrow
                if (lastKey = 132 & ~(direction = 3)) { let direction = 4; }   // right arrow

                let head = snake.getHead();
                let eqX = head.getX();
                let eqY = head.getY();
                let feqX = food.getX();
                let feqY = food.getY();

                let collided = detectColision(head);
                if(collided){
                    return score;
                }

                if(~(eqX = feqX & eqY = feqY)){
                    do undrawTail();
                    do snake.move(direction);
                    do drawHead();
                }else{
                    do despawnFood();
                    do spawnFood();
                    do snake.grow(direction);
                    do drawHead();
                }

                let moveCounter = 0;
            } else {
                let moveCounter = moveCounter + 1;
            }
            do Sys.wait(100);
        }
        return score;
    }

    method void spawnFood(){
        var int x;
        var int y;

        do LCGRandom.setSeed(seed);
        let x = LCGRandom.randRange(2, 60);
        let y = LCGRandom.randRange(2, 30);

        let food = Coordinate.new(x, y);
        do drawCoordinate(food, true);
        let seed = seed + 1;
        return;
    }

    method boolean detectColision(Coordinate coord){
        var Array snakeArray;
        var int snakeX;
        var int snakeY;
        var Coordinate snakeCord;
        var int currentX;
        var int currentY;
        var int size;
        var int i;

        let i = 0;
        let size = snake.getSize();
        let snakeArray = snake.getCoordinatesArray();
        let currentX = coord.getX();
        let currentY = coord.getY();
        while(i < (size - 1)){
            let snakeCord = snakeArray[i];
            let snakeX = snakeCord.getX();
            let snakeY = snakeCord.getY();
            if ((snakeX = currentX) & (snakeY = currentY)){
                return true;
            }
            let i = i + 1;
        }
        return false;
    }

    method void despawnFood(){
        let score = score + 1;
        do food.dispose();
        return;
    }


    method void drawSnake(){
        var Array coordinatesArray;
        var int i;
        
        let coordinatesArray = snake.getCoordinatesArray();

        let i = 0;
        while (i < snake.getSize()){
            do drawCoordinate(coordinatesArray[i], true);
            let i = i + 1;
        }
        return;
    }

    // Draws the Snake's current head.
    method void drawHead(){
        do drawCoordinate(snake.getHead(), true);
        return;
    }

    // Erases the Snake's currrent tail.
    method void undrawTail(){
        do drawCoordinate(snake.getTail(), false);
        return;
    }

    // Draws or erases the given coordinate.
    method void drawCoordinate(Coordinate coord, boolean draw){
        var int coordX;
        var int coordY;
        let coordX = coord.getX();
        let coordY = coord.getY();
        do Screen.setColor(draw);
        do Screen.drawRectangle(coordX*8, coordY*8, (coordX*8)+8, (coordY*8)+8);
        do Screen.setColor(false);
        return;
    }

    // Draws the border of the game. The border will have a thickness of borderWidth pixels.
    method void drawBorder(int borderWidth){
        do Screen.setColor(true);
        do Screen.drawRectangle(0, 0, 511, borderWidth);                                // North Border
        do Screen.drawRectangle(0, 255-borderWidth, 511, 255);                          // South Border
        do Screen.drawRectangle(0, borderWidth+1, borderWidth, 255-borderWidth);        // West Border
        do Screen.drawRectangle(511-borderWidth, borderWidth+1, 511, 255-borderWidth);  // East Border
        do Screen.setColor(false);
        return;
    }

    // Disposes of the SnakeGame object.
    method void dispose(){
        do Memory.deAlloc(this);
        return;
    }
    
}
